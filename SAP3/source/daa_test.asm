	.org 0

;
;************************************************************
;                8080/8085 CPU TEST/DIAGNOSTIC
;************************************************************
;
;NOTE: (1) PROGRAM ASSUMES "CALL",AND "LXI SP" INSTRUCTIONS WORK!
;
;      (2) INSTRUCTIONS NOT TESTED ARE "HLT","DI","EI","RIM","SIM",
;          AND "RST 0" THRU "RST 7"
;
;
;
;	DAA Testing
;
CPU:	LXI	SP,STACK	;SET THE STACK POINTER
	ANI	0	;INITIALIZE A REG. AND CLEAR ALL FLAGS
	MVI	A,0AAH
	CMA	
	CPI	055H
	CNZ	CPUER	;TEST "CMA"
	ORA	A	;RE-SET AUXILIARY CARRY
	DAA
	CPI	055H
	CNZ	CPUER	;TEST "DAA"
	MVI	A,088H
	ADD	A
	DAA
	CPI	076H
	CNZ	CPUER	;TEST "DAA"
	XRA	A
	MVI	A,0AAH
	DAA
	CNC	CPUER	;TEST "DAA"
	CPI	010H
	CNZ	CPUER	;TEST "DAA"
	XRA	A
	MVI	A,09AH
	DAA
	CNC	CPUER	;TEST "DAA"
	CNZ	CPUER	;TEST "DAA"
;
;	Roll Testing
;
	STC
	MVI	A,042H
	RLC
	CC	CPUER	;TEST "RLC" FOR RE-SET CARRY
	RLC
	CNC	CPUER	;TEST "RLC" FOR SET CARRY
	CPI	009H
	CNZ	CPUER	;TEST "RLC" FOR ROTATION
	RRC
	CNC	CPUER	;TEST "RRC" FOR SET CARRY
	RRC
	CPI	042H
	CNZ	CPUER	;TEST "RRC" FOR ROTATION
	RAL
	RAL
	CNC	CPUER	;TEST "RAL" FOR SET CARRY
	CPI	008H
	CNZ	CPUER	;TEST "RAL" FOR ROTATION
	RAR
	RAR
	CC	CPUER	;TEST "RAR" FOR RE-SET CARRY
	CPI	002H
	CNZ	CPUER	;TEST "RAR" FOR ROTATION
;
	LXI	HL,CPUOK
	PCHL		;TEST "PCHL"
;
CPUER:	MVI A,255
	OUT 3
	HLT
;
CPUOK:
	MVI A,1
	OUT 3
	HLT


	.org 0500H
;
TEMPP:	.word	0502H	;POINTER USED TO TEST "LHLD","SHLD",
			; AND "LDAX" INSTRUCTIONS
;
TEMP0:	.byte 0	;TEMPORARY STORAGE FOR CPU TEST MEMORY LOCATIONS
TEMP1:	.byte 0	;TEMPORARY STORAGE FOR CPU TEST MEMORY LOCATIONS
TEMP2:	.byte 0	;TEMPORARY STORAGE FOR CPU TEST MEMORY LOCATIONS
TEMP3:	.byte 0	;TEMPORARY STORAGE FOR CPU TEST MEMORY LOCATIONS
TEMP4:	.byte 0	;TEMPORARY STORAGE FOR CPU TEST MEMORY LOCATIONS
SAVSTK:	.word 0	;TEMPORARY STACK-POINTER STORAGE LOCATION

	.org 0700H
STACK:	.byte 0

	.end
